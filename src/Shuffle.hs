module Shuffle (shuffle, shuffle') where
import System.Random
import Data.Array.IO
import Control.Monad

import Data.Array.ST
import Control.Monad.ST
import Data.STRef
import Control.Exception


-- | Randomly shuffle a list
--   /O(N)/
shuffle_old :: [a] -> IO [a]
shuffle_old xs = do
        ar <- newArray n xs
        forM [1..n] $ \i -> do
            j <- randomRIO (i,n)
            vi <- readArray ar i
            vj <- readArray ar j
            writeArray ar j vi
            return vj
  where
    n = length xs
    newArray :: Int -> [a] -> IO (IOArray Int a)
    newArray n xs =  newListArray (1,n) xs


shuffle :: [a] -> Maybe StdGen -> IO [a]
shuffle x Nothing = getStdRandom (shuffle' x)
shuffle x (Just stdGen) = evaluate $ fst (shuffle' x stdGen)

shuffle' :: [a] -> StdGen -> ([a],StdGen)
shuffle' xs gen = runST (do
        g <- newSTRef gen
        let randomRST lohi = do
              (a,s') <- liftM (randomR lohi) (readSTRef g)
              writeSTRef g s'
              return a
        ar <- newArray n xs
        xs' <- forM [1..n] $ \i -> do
                j <- randomRST (i,n)
                vi <- readArray ar i
                vj <- readArray ar j
                writeArray ar j vi
                return vj
        gen' <- readSTRef g
        return (xs',gen'))
  where
    n = length xs
    newArray :: Int -> [a] -> ST s (STArray s Int a)
    newArray n xs =  newListArray (1,n) xs
